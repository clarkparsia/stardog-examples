apply plugin: 'java'

ext {
	stardogVersion = project.hasProperty("stardogVersion") ? project.getProperty("stardogVersion") : '9.0.0'
	projectVersion = '0.0.6'

	distPath = "${projectDir}/dist"
	stardogRepo = project.hasProperty("stardogRepo") ? project.getProperty("stardogRepo") : "http://maven.stardog.com"
	stardogLib = project.hasProperty("stardogLib") ? project.getProperty("stardogLib") : "."

	guavaVersion = "33.2.1-jre"
	junitVersion = "4.12"
	rdf4jRuntimeVersion = "2.2.4"
	slf4jVersion = "2.0.13"
}

allprojects {
	apply plugin: 'idea'
	group = 'com.complexible.stardog'
	version = projectVersion
	// also configure in idea.gradle
	sourceCompatibility = '11'
	targetCompatibility = '11'

	repositories {
		maven {
			url stardogRepo
		}
		mavenLocal()
		mavenCentral()
		maven {
			url 'https://jitpack.io'
		}

	}

	tasks.withType(Test) {
		systemProperty 'stardog.home', rootProject.projectDir
		systemProperty 'java.library.path', project.stardogLib
	}
}

subprojects {
	apply plugin: "java"

	// common deps
	dependencies {
		implementation("com.google.guava:guava:$guavaVersion") {
			version {
				require "$guavaVersion"
			}
			exclude group: "com.google.code.findbugs"
			exclude group: "com.google.guava", module: "failureaccess"
		}
		implementation "org.slf4j:slf4j-api:$slf4jVersion"
		implementation "org.slf4j:slf4j-jdk14:$slf4jVersion"

		testImplementation "junit:junit:$junitVersion"
	}

	sourceSets {
		all {
			// work around a Gradle-metadata bug introduced in Guava v32.1.0
			configurations.all {
				attributes.attribute(Attribute.of("org.gradle.jvm.environment", String), "standard-jvm")
			}
		}
		main {
			java {
				srcDir 'main/src'
			}
			resources {
				srcDir 'main/resources'
			}
		}
		test {
			java {
				srcDir 'test/src'
			}
			resources {
				srcDir 'test/resources'
			}
		}
	}
}
